apply plugin: 'com.android.library'
apply plugin: 'robolectric'
apply plugin: 'maven'

dependencies {
    compile project(':third_party:gif_decoder')
    compile project(':third_party:disklrucache')

    compile 'com.android.support:support-v4:19.1.+'

    androidTestCompile 'org.hamcrest:hamcrest-core:1.3'
    androidTestCompile 'junit:junit:4.11'
    androidTestCompile 'org.mockito:mockito-all:1.9.5'
    androidTestCompile 'org.robolectric:robolectric:2.4-SNAPSHOT'
}

android {
    compileSdkVersion 19
    buildToolsVersion = '19.1.0'

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 19
        versionCode = VERSION_CODE
        versionName = VERSION_NAME
    }

    sourceSets {
        main {
            java.srcDirs         = ['src/main/java']
            manifest.srcFile 'AndroidManifest.xml'
        }

        androidTest {
            java.srcDirs = ['src/test/java']
        }
    }
}

apply from: "$rootProject.projectDir/scripts/upload.gradle"

// All dependencies are written to the pom file with the default type (.jar). Since we're uploading the gif decoder
// as an aar we need to change the type in the pom file to match or maven/gradle wont' be able to find the transitive
// dependency.
afterEvaluate {
    def deployer = uploadArchives.repositories.mavenDeployer

    [deployer]*.pom*.whenConfigured { pom ->
        pom.dependencies.find { dep ->
            dep.groupId == 'com.github.bumptech.glide' && dep.artifactId == 'glide-gif-decoder'
        }.type = "aar"
    }
}
